import "data/script/wait.gr"

task moveSkull(int enemyIdx) {
    let baseX, baseY = getPosition(enemyIdx);
    let goesLeft = true;

    loop {
        let x, y = getPosition(enemyIdx);
        let movX = 0f;

    	if(goesLeft) {
    		movX = -1f;
    	} else {
    		movX = 1f;
    	}

        setMovementSpeed(enemyIdx, movX, 0f);
        yield;

        // Check enemy is still alive after the yield
        if(!isAlive(enemyIdx)) {
            setMovementSpeed(enemyIdx, 0f, 0f);
        	kill;
        }

        if(goesLeft and (x < (baseX - 100f))) {
    		goesLeft = false;
    	}

    	if(!goesLeft and (x > (baseX + 100f))) {
    		goesLeft = true;
    	}
    }
}

task fireCircle(int enemyIdx) {
    int nbShots = 10;
    float angleRange = 360f;
    float step = (angleRange / (nbShots as float));
    float radius = 40f;
    float speed  = 5f;

    loop {
        let x, y = getPosition(enemyIdx);
        float angle = 0f;
        loop (nbShots) {
            fireShot(enemyIdx,
                x + cos(angle) * radius,
                y + sin(angle) * radius,
                angle,
                speed);

            angle = angle + step;
        }

        wait(120);

        // Check enemy is still alive after the yield
        if(!isAlive(enemyIdx)) {
            kill;
        }
    }
}